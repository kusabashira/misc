#!/usr/bin/perl
use strict;
use warnings;
use utf8;
use autodie qw(:all);
use Path::Tiny qw(path);
use File::Temp qw(tempfile);
use File::Basename qw(basename);
use Getopt::Long qw(:config posix_default no_ignore_case gnu_compat bundling);

our $NAME    = basename $0;
our $VERSION = "0.2.1";
our $USAGE   = <<__USAGE__;
Usage: $NAME [OPTION]... {script-only-if-no-other-script} [input-file]...
Edit text with Vim script.

Options:
  -n, --quiet, --silent
                 suppress automatic printing of buffer
  -e script, --expression=script
                 add the script to the commands to be executed
  -f script-file, --file=script-file
                 add the contents of script-file to the commands to be executed
  --help
                 display this help and exit
  --version
                 display version information and exit
__USAGE__

my @expressions;
my $require_print = 1;
my $require_program = 1;
GetOptions(
  "quiet|silent|n" => sub {
    $require_print = 0;
  },
  "expression|e=s" => sub {
    my (undef, $expression) = @_;
    push @expressions, "$expression\n";
    $require_program = 0;
  },
  "file|f=s" => sub {
    my (undef, $file) = @_;
    push @expressions, path($file)->lines_utf8;
    $require_program = 0;
  },
  "help" => sub {
    print STDERR $USAGE;
    exit 0;
  },
  "version" => sub {
    print STDERR "$VERSION\n";
    exit 0;
  },
) or exit 2;

if ($require_program) {
  if (@ARGV < 1) {
    print STDERR $USAGE;
    exit 2;
  }
  my $expression = shift @ARGV;
  push @expressions, "$expression\n";
}
if ($require_print) {
  push @expressions, "%print\n";
}
push @expressions, "qall!\n";

my (undef, $src) = tempfile(UNLINK => 1);
my (undef, $script_file) = tempfile(UNLINK => 1);
path($src)->spew_utf8(<<>>);
path($script_file)->spew_utf8(@expressions);

system "vim", "-u", "NONE", "-i", "NONE",
  "-N", "-n", "-e", "-s", "-S", $script_file, "--", $src;
